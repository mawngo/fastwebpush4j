plugins {
    id "io.freefair.lombok" version "${lombokPluginVersion}"
    id 'java-library'
    id 'java'
    id 'net.ltgt.errorprone' version '4.0.1'
    id 'idea'
    id 'com.vanniktech.maven.publish' version '0.34.0'
}

group "${projectGroup}"
version "${projectVersion}"


java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

allprojects {
    repositories {
        mavenCentral()
    }
}

publishing {
    repositories {
        maven {
            name = "build"
            def releasesRepoUrl = layout.buildDirectory.dir('repos/releases')
            def snapshotsRepoUrl = layout.buildDirectory.dir('repos/snapshots')
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

mavenPublishing {
    publishToMavenCentral(true)
    if (project.hasProperty("signing.keyId") || System.getenv("ORG_GRADLE_PROJECT_signingInMemoryKey") != null) {
        signAllPublications()
    }

    coordinates("${projectGroup}", "${projectName}", "${projectVersion}")

    pom {
        name = "${aboutName}"
        description = "${aboutDescription}"
        url = project.properties["aboutPage"] ?: "https://${projectGit}"
        licenses {
            license {
                name = 'MIT License'
                url = 'https://opensource.org/licenses/MIT'
            }
        }
        developers {
            developer {
                id = "${aboutDeveloperId}"
                name = "${aboutDeveloperName}"
                email = "${aboutDeveloperEmail}"
            }
        }
        scm {
            connection = "scm:git:git:${projectGit}.git"
            developerConnection = "scm:git:ssh://${projectGit}.git"
            url = "https://${projectGit}"
        }
    }
}

dependencies {
    errorprone "com.google.errorprone:error_prone_core:$errorProneVersion"
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    api 'org.eclipse.jetty:jetty-client:12.0.10'
    api 'org.eclipse.jetty.http2:jetty-http2-client-transport:12.0.10'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.70'
    implementation 'org.bitbucket.b_c:jose4j:0.7.9'
}

tasks.withType(JavaCompile).configureEach {
    options.errorprone.disableWarningsInGeneratedCode = true
    options.errorprone.errorproneArgs = [
        '-Xep:UnusedMethod:OFF',
        '-Xep:JavaTimeDefaultTimeZone:OFF'
    ]
}

jar {
    archiveClassifier = ''
    enabled = true
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
